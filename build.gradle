
plugins {
    id 'java'
    id 'checkstyle'
    id 'uk.gov.hmcts.java' version '0.12.33'
    id "io.freefair.lombok" version "5.3.3.3"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
    // id 'pmd'
    id 'jacoco'
}


def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: getCurrentGitBranch()

group 'uk.gov.hmcts.reform.pcq'
// Do not change this version number
version buildNumber


sourceCompatibility = 1.11

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/pcq-commons"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task unit(type: Test, description: 'Runs the unit tests.', group: 'Verification') {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'pcq-commons'
            version project.version
        }
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.29'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

def versions = [
        jupiter           : '5.9.1',
        lombok            : '1.18.8',
        springfoxSwagger  : '2.9.2',
        springBoot        : '2.3.4.RELEASE'
]

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

    compile group: 'org.springframework', name: 'spring-core', version: '5.3.23'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.7.RELEASE'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    compile group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
    compile group: 'io.github.openfeign.form', name: 'feign-form-spring', version: '3.8.0'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4'

    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    compile group: "com.gilecode.yagson", name:"j9-reflection-utils", version:"1.0"

    implementation group: "io.jsonwebtoken", name: "jjwt", version: "0.9.1"

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testCompile group: 'org.mockito', name: 'mockito-inline', version: '3.12.4'

    testCompile("org.junit.jupiter:junit-jupiter-api:5.9.1")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.9.1")
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.1')
    testImplementation('org.mockito:mockito-core:2.28.2')

    testImplementation group: "com.gilecode.yagson", name:"j9-reflection-utils", version:"1.0"
    testImplementation group: "io.jsonwebtoken", name: "jjwt", version: "0.9.1"


}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    systemProperty "user.timezone", "UTC"
}

jar {
    enabled = true
}

def getCurrentGitBranch() {
    def gitBranch = "DEV-SNAPSHOT"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}